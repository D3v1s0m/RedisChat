plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'java'
}


group = 'dev.unnm3d'
version = '4.1.4'


repositories {
    mavenLocal()
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.xenondevs.xyz/releases' }
    maven { url 'https://repo.maven.apache.org/maven2/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
}

dependencies {
    implementation 'net.kyori:adventure-text-minimessage:4.14.0'
    implementation 'net.kyori:adventure-text-serializer-plain:4.14.0'
    implementation 'com.github.Emibergo02.ConfigLib:configlib-paper:master-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.1'
    implementation 'xyz.xenondevs.invui:invui-core:1.18'
    implementation 'xyz.xenondevs.invui:inventory-access-r5:1.18'
    implementation 'xyz.xenondevs.invui:inventory-access-r7:1.18'
    implementation 'xyz.xenondevs.invui:inventory-access-r9:1.18'
    implementation 'xyz.xenondevs.invui:inventory-access-r13:1.18'
    implementation 'xyz.xenondevs.invui:inventory-access-r14:1.18'
    implementation 'xyz.xenondevs.invui:inventory-access-r15:1.18'
    implementation 'dev.jorel:commandapi-bukkit-shade:9.2.0'

    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'io.lettuce:lettuce-core:6.2.5.RELEASE'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'
    compileOnly 'org.mariadb.jdbc:mariadb-java-client:3.1.4'
    compileOnly 'com.h2database:h2:2.2.220'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'com.github.oraxen:oraxen:1.162.0'
    compileOnly 'com.github.LeonMangler:PremiumVanishAPI:2.8.8'
    compileOnly 'org.projectlombok:lombok:1.18.26'

    annotationProcessor('org.projectlombok:lombok:1.18.26')
}

shadowJar {
    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')
    dependencies {
        include dependency('com.github.Emibergo02.ConfigLib:configlib-paper:master-SNAPSHOT')
        include dependency('com.github.Emibergo02.ConfigLib:configlib-yaml:master-SNAPSHOT')
        include dependency("com.github.Emibergo02.ConfigLib:configlib-core:master-SNAPSHOT")
        include dependency('net.kyori:adventure-api:4.14.0')
        include dependency('net.kyori:adventure-key:4.14.0')
        include dependency('net.kyori:adventure-nbt:4.13.1')
        include dependency('net.kyori:adventure-platform-api:4.3.1')
        include dependency('net.kyori:adventure-platform-bukkit:4.3.1')
        include dependency('net.kyori:adventure-platform-facet:4.3.1')
        include dependency('net.kyori:adventure-text-minimessage:4.14.0')
        include dependency('net.kyori:adventure-text-serializer-plain:4.14.0')
        include dependency('net.kyori:adventure-text-serializer-legacy:4.13.1')
        include dependency('net.kyori:adventure-text-serializer-gson:4.13.1')
        include dependency('net.kyori:adventure-text-serializer-gson-legacy-impl:4.13.1')
        include dependency('net.kyori:adventure-text-serializer-bungeecord:4.3.1')
        include dependency('xyz.xenondevs.invui:invui-core:1.18')
        include dependency('xyz.xenondevs.invui:inventory-access:1.18')
        include dependency('xyz.xenondevs.invui:inventory-access-r5:1.18')
        include dependency('xyz.xenondevs.invui:inventory-access-r7:1.18')
        include dependency('xyz.xenondevs.invui:inventory-access-r9:1.18')
        include dependency('xyz.xenondevs.invui:inventory-access-r13:1.18')
        include dependency('xyz.xenondevs.invui:inventory-access-r14:1.18')
        include dependency('xyz.xenondevs.invui:inventory-access-r15:1.18')
        include dependency('dev.jorel:commandapi-bukkit-shade:9.2.0')
    }
    exclude 'colors.bin'
    exclude 'net.kyori.adventure.title'
    exclude 'net.kyori.adventure.bossbar'
    exclude 'net.kyori.adventure.translation'
    relocate 'net.kyori.adventure', 'dev.unnm3d.redischat.libraries.adventure'
    relocate 'de.exlll.configlib', 'dev.unnm3d.redischat.libraries.configlib'
    relocate 'xyz.xenondevs.invui', 'dev.unnm3d.redischat.libraries.invui'
    relocate 'xyz.xenondevs.inventoryaccess', 'dev.unnm3d.redischat.libraries.inventoryaccess'
    relocate 'dev.jorel.commandapi', 'dev.unnm3d.redischat.libraries.commandapi'
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    withSourcesJar()
    withJavadocJar()

}

tasks.withType(JavaCompile).configureEach {
    options.release.set targetJavaVersion
    options.encoding = 'UTF-8'
}


processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'dev.unnm3d'
            artifactId = 'redischat'
            version = "$version"
            artifact shadowJar
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

jar.dependsOn shadowJar
clean.delete "$rootDir/target"